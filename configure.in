AC_INIT(vink,1.0.0,[Morten Hustveit <morten@rashbox.org>])
AM_INIT_AUTOMAKE

AC_CONFIG_MACRO_DIR([m4])

AC_PROG_CC
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_MAKE_SET

AC_CHECK_FILE([/etc/pki/tls/cert.pem], [CAFILE=/etc/pki/tls/cert.pem], 
  AC_CHECK_FILE([/etc/ssl/certs/ca-certificates.crt], [CAFILE=/etc/ssl/certs/ca-certificates.crt],
                [AC_MSG_ERROR([no certificate authority file found])]))

AC_DEFUN([AS_AC_EXPAND],
[
  EXP_VAR=[$1]
  FROM_VAR=[$2]

  dnl first expand prefix and exec_prefix if necessary
  prefix_save=$prefix
  exec_prefix_save=$exec_prefix

  dnl if no prefix given, then use /usr/local, the default prefix
  if test "x$prefix" = "xNONE"; then
    prefix="$ac_default_prefix"
  fi
  dnl if no exec_prefix given, then use prefix
  if test "x$exec_prefix" = "xNONE"; then
    exec_prefix=$prefix
  fi

  full_var="$FROM_VAR"
  dnl loop until it doesn't change anymore
  while true; do
    new_full_var="`eval echo $full_var`"
    if test "x$new_full_var" = "x$full_var"; then break; fi
    full_var=$new_full_var
  done

  dnl clean up
  full_var=$new_full_var
  AC_SUBST([$1], "$full_var")

  dnl restore prefix and exec_prefix
  prefix=$prefix_save
  exec_prefix=$exec_prefix_save
])

AC_DEFINE(_GNU_SOURCE, [1], [GNU C Library extensions])
AC_DEFINE_UNQUOTED(CA_CERT_FILE, ["$CAFILE"], [Path to certificate authority database])

AS_AC_EXPAND(SYSCONFDIR, $sysconfdir)
AC_DEFINE_UNQUOTED(SYSCONFDIR, ["$SYSCONFDIR"], [Path to system configuration])

AS_AC_EXPAND(LOCALSTATEDIR, $localstatedir)
AC_DEFINE_UNQUOTED(LOCALSTATEDIR, ["$LOCALSTATEDIR"], [Path to local state])

AS_AC_EXPAND(SRCDIR, $srcdir)
AC_DEFINE_UNQUOTED(SRCDIR, ["$SRCDIR"], [Path to source files])

AC_CONFIG_HEADERS([config.h])
AC_OUTPUT(Makefile)
